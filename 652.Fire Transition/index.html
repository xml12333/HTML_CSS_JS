<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>On-Scroll Fire Transition (WebGL + GSAP ScrollTrigger)</title>
  <link rel="stylesheet" href="./css/style.css">

</head>
<body>
<div class="page">
    <div class="header">
        How it's done
    </div>
    <div class="content">
        <p>
            The HTML content you're reading right now is overlaid with a full-screen <b>&lt;canvas&gt;</b> element.
            There is a fragment shader that defines opacity and color for each pixel of the <b>&lt;canvas&gt;</b>.
            Shader input values are <b>animation progress</b>, <b>time</b>, and <b>resolution</b>.
        </p>
        <p>
            While <b>time</b> and <b>window size (resolution)</b> are super easy to gather, for <b>animation progress</b> I use <a href="https://gsap.com/docs/v3/Plugins/ScrollTrigger/" target="_blank">GSAP ScrollTrigger</a> plugin.
        </p>
        <p>
            Once the inputs are prepared, we pass them as uniforms to the shader.
            The WebGL part of this demo is a basic JS boilerplate to render a fragment shader on the single full-screen plane. No extra libraries here.
        </p>
        <p>
            The fragment shader is based on <a href="https://thebookofshaders.com/13/" target="_blank">Fractal Brownian Motion (fBm)</a> noise.
        </p>
        <p>
            First, we create a semi-transparent mask to define a contour of burning paper. It is basically a low-scale fBm noise with <b>animation progress</b> value used as a threshold.
            Taking the same fBm noise with different thresholds we can
            <br>
            (a) darken parts of the paper so each pixel gets darker before turning transparent
            <br>
            (b) define the stripe along the paper edge for fire.
        </p>
        <p>
            The fire is done as another two fBm based functions, one for shape and one for color. Both have a higher scale and both are animated with <b>time</b> value instead of <b>animation progress</b>.
        </p>

        
    </div>
</div>

<canvas id="fire-overlay"></canvas>
<div class="scroll-msg">
    <div>Hello ðŸ‘‹</div>
    <div>scroll me</div>
    <div class="arrow-animated">&darr;</div>
</div>

<script type="x-shader/x-fragment" id="vertShader">
    precision mediump float;

    varying vec2 vUv;
    attribute vec2 a_position;

    void main() {
        vUv = a_position;
        gl_Position = vec4(a_position, 0.0, 1.0);
    }
</script>


<script type="x-shader/x-fragment" id="fragShader">
    precision mediump float;

    varying vec2 vUv;
    uniform vec2 u_resolution;
    uniform float u_progress;
    uniform float u_time;

    float rand(vec2 n) {
        return fract(cos(dot(n, vec2(12.9898, 4.1414))) * 43758.5453);
    }
    float noise(vec2 n) {
        const vec2 d = vec2(0., 1.);
        vec2 b = floor(n), f = smoothstep(vec2(0.0), vec2(1.0), fract(n));
        return mix(mix(rand(b), rand(b + d.yx), f.x), mix(rand(b + d.xy), rand(b + d.yy), f.x), f.y);
    }
    float fbm(vec2 n) {
        float total = 0.0, amplitude = .4;
        for (int i = 0; i < 4; i++) {
            total += noise(n) * amplitude;
            n += n;
            amplitude *= 0.6;
        }
        return total;
    }

    void main() {
        vec2 uv = vUv;
        uv.x *= min(1., u_resolution.x / u_resolution.y);
        uv.y *= min(1., u_resolution.y / u_resolution.x);

        float t = u_progress;
        vec3 color = vec3(1., 1., .95);

        float main_noise = 1. - fbm(.75 * uv + 10. - vec2(.3, .9 * t));

        float paper_darkness = smoothstep(main_noise - .1, main_noise, t);
        color -= vec3(.99, .95, .99) * paper_darkness;

        vec3 fire_color = fbm(6. * uv - vec2(0., .005 * u_time)) * vec3(6., 1.4, .0);
        float show_fire = smoothstep(.4, .9, fbm(10. * uv + 2. - vec2(0., .005 * u_time)));
        show_fire += smoothstep(.7, .8, fbm(.5 * uv + 5. - vec2(0., .001 * u_time)));

        float fire_border = .02 * show_fire;
        float fire_edge = smoothstep(main_noise - fire_border, main_noise - .5 * fire_border, t);
        fire_edge *= (1. - smoothstep(main_noise - .5 * fire_border, main_noise, t));
        color += fire_color * fire_edge;

        float opacity = 1. - smoothstep(main_noise - .0005, main_noise, t);

        gl_FragColor = vec4(color, opacity);
    }

</script>
<!-- partial -->
  <script src='./js/gsap.min.js'></script>
<script src='https://unpkg.com/gsap@3/dist/ScrollTrigger.min.js'></script><script  src="./js/script.js"></script>

</body>
</html>
